SUMMARY="Basic networking tools"
LICENSE="GPL-2.0+"

COMPATIBLE_HOST_ARCHS = ".*linux"

inherit c make

#
# NOTE:
#   This code currently disbles gettext/i18n! This can be fixed by rerunning
#   make config and say NO to gettext. I will leave this excercise for someone
#   else.
#   Change/generate net-tools-1.60-config.{h,make} to enable/disable features
#   and gettext
#
#

require conf/fetch/sourceforge.conf
SRC_URI = "${SOURCEFORGE_MIRROR}/project/${PN}.berlios/${P}.tar.bz2"

SRC_URI += "\
	file://net-tools-${PV}-config.h \
	file://net-tools-${PV}-config.make \
	file://ether-wake.c \
	file://ether-wake.8 \
	file://mii-diag.c \
	file://mii-diag.8 \
	file://net-tools-1.57-bug22040.patch \
	file://net-tools-1.60-miiioctl.patch \
	file://net-tools-1.60-manydevs.patch;striplevel=0 \
	file://net-tools-1.60-virtualname.patch \
	file://net-tools-1.60-cycle.patch \
	file://net-tools-1.60-nameif.patch \
	file://net-tools-1.60-ipx.patch \
	file://net-tools-1.60-inet6-lookup.patch \
	file://net-tools-1.60-man.patch \
	file://net-tools-1.60-gcc33.patch \
	file://net-tools-1.60-trailingblank.patch \
	file://net-tools-1.60-interface.patch \
	file://net-tools-1.60-gcc34.patch \
	file://net-tools-1.60-overflow.patch \
	file://net-tools-1.60-siunits.patch \
	file://net-tools-1.60-trunc.patch \
	file://net-tools-1.60-return.patch \
	file://net-tools-1.60-parse.patch \
	file://net-tools-1.60-netmask.patch \
	file://net-tools-1.60-ulong.patch \
	file://net-tools-1.60-bcast.patch \
	file://net-tools-1.60-mii-tool-obsolete.patch \
	file://net-tools-1.60-netstat_ulong.patch \
	file://net-tools-1.60-note.patch \
	file://net-tools-1.60-num-ports.patch \
	file://net-tools-1.60-duplicate-tcp.patch \
	file://net-tools-1.60-statalias.patch \
	file://net-tools-1.60-isofix.patch \
	file://net-tools-1.60-ifconfig_ib.patch \
	file://net-tools-1.60-de.patch \
	file://net-tools-1.60-pie.patch \
	file://net-tools-1.60-ifaceopt.patch \
	file://net-tools-1.60-trim_iface.patch \
	file://net-tools-1.60-stdo.patch \
	file://net-tools-1.60-statistics.patch \
	file://net-tools-1.60-ifconfig.patch \
	file://net-tools-1.60-arp_overflow.patch \
	file://net-tools-1.60-hostname_man.patch \
	file://net-tools-1.60-interface_stack.patch;striplevel=0 \
	file://net-tools-1.60-selinux.patch \
	file://net-tools-1.60-netstat_stop_trim.patch \
	file://net-tools-1.60-STRIP.patch \
	file://net-tools-1.60-if_tr.patch \
"

TOPDIR[unexport] = "1"

# The Makefile is lame, no parallel build
PARALLEL_MAKE = ""

inherit auto-package-utils
AUTO_PACKAGE_UTILS = "\
mii-diag \
mii-tool \
arp \
ether-wake \
ifconfig \
ipmaddr \
iptunnel \
nameif \
plipconfig \
rarp \
route \
slattach \
hostname \
netstat \
"
AUTO_PACKAGE_UTILS_RDEPENDS = "libc libdl"

FILES_${PN}-hostname = "\
${base_bindir}/dnsdomainname \
${base_bindir}/domainname \
${base_bindir}/nisdomainname \
${base_bindir}/ypdomainname \
"
RDEPENDS_${PN}-ifconfig += "libgcc"
RDEPENDS_${PN}-netstat += "libgcc"
RDEPENDS_${PN} += "${AUTO_PACKAGE_UTILS_PACKAGES}"

# Copy config and source over to the source directory
do_configure() {
    cp ${SRCDIR}/net-tools-${PV}-config.h    ${S}/config.h
    cp ${SRCDIR}/net-tools-${PV}-config.make ${S}/config.make
    cp ${SRCDIR}/ether-wake.c ${S}
    cp ${SRCDIR}/ether-wake.8 ${S}/man/en_US
    cp ${SRCDIR}/mii-diag.c   ${S}
    cp ${SRCDIR}/mii-diag.8   ${S}/man/en_US
}

do_compile() {
    export COPTS="$CFLAGS"
    export LOPTS="$LDFLAGS"
    unset CFLAGS
    unset LDFLAGS

    oe_runmake
    $CC $COPTS -o ether-wake ether-wake.c
    $CC $COPTS -o  mii-diag   mii-diag.c
}

do_install() {
    oe_runmake 'BASEDIR=${D}' install

    install -m 0755 ${S}/mii-diag ${D}/${base_sbindir}/
    install -m 0755 ${S}/ether-wake ${D}/${base_sbindir}/
}
